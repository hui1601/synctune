plugins {
    id 'java-library'
}

group = 'ac.cwnu'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    // SyncTune SDK
    api project(':sdk')
    
    // JavaFX 의존성
    def javafxVersion = '21.0.1'
    def osName = org.gradle.internal.os.OperatingSystem.current()

    def javafxPlatform = ''
    if (osName.isWindows()) {
        javafxPlatform = 'win'
    } else if (osName.isMacOsX()) {
        javafxPlatform = 'mac'
    } else if (osName.isLinux()) {
        javafxPlatform = 'linux'
    }

    ['base', 'graphics', 'controls', 'fxml'].each { mod ->
        implementation "org.openjfx:javafx-${mod}:${javafxVersion}:${javafxPlatform}"
    }
    
    // 테스트 의존성
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

test {
    useJUnitPlatform()
}

// JavaFX 모듈 패스 설정
tasks.withType(JavaCompile) {
    options.compilerArgs += ['--module-path', configurations.runtimeClasspath.asPath]
}

tasks.withType(Test) {
    jvmArgs += ['--module-path', configurations.testRuntimeClasspath.asPath]
}